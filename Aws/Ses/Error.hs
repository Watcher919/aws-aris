{-# LANGUAGE DeriveDataTypeable, MultiParamTypeClasses, RecordWildCards #-}
module Aws.Ses.Error
    ( SesErrorCode(..)
    , toSesErrorCode
    , SesError(..)
    ) where

import           Data.Typeable
import qualified Control.Exception         as C
import qualified Network.HTTP.Types        as HTTP


-- | An error code that may be generated by an Amazon SES
-- command.  The error code descriptions below are taken from
-- Amazon's documentation at
-- <http://docs.amazonwebservices.com/ses/latest/APIReference/CommonErrors.html>.
data SesErrorCode =
    SesIncompleteSignature
  -- ^ The request signature does not conform to AWS standards.
  | SesInternalFailure
  -- ^ The request processing has failed due to some unknown
  -- error, exception or failure.
  | SesInvalidAction
  -- ^ The action or operation requested is invalid.
  | SesInvalidClientTokenId
  -- ^ The X.509 certificate or AWS Access Key ID provided does
  -- not exist in our records.
  | SesInvalidParameterCombination
  -- ^ Parameters that must not be used together were used
  -- together.
  | SesInvalidParameterValue
  -- ^ A bad or out-of-range value was supplied for the input
  -- parameter.
  | SesInvalidQueryParameter
  -- ^ AWS query string is malformed, does not adhere to AWS
  -- standards.
  | SesMalformedQueryString
  -- ^ The query string is malformed.
  | SesMissingAction
  -- ^ The request is missing an action or operation parameter.
  | SesMissingAuthenticationToken
  -- ^ Request must contain either a valid (registered) AWS
  -- Access Key ID or X.509 certificate.
  | SesMissingParameter
  -- ^ An input parameter that is mandatory for processing the
  -- request is not supplied.
  | SesOptInRequired
  -- ^ The AWS Access Key ID needs a subscription for the
  -- service.
  | SesRequestExpired
  -- ^ Request is past expires date or the request date (either
  -- with 15 minute padding), or the request date occurs more
  -- than 15 minutes in the future.
  | SesServiceUnavailable
  -- ^ The request has failed due to a temporary failure of the
  -- server.
  | SesThrottling
  -- ^ Request was denied due to request throttling.
  | SesMessageRejected
  -- ^ Indicates that the action failed, and the message could
  -- not be sent. Check the error stack for more information
  -- about what caused the error.
  | SesOtherError String
  -- ^ Another error code unknown to @aws@ package.
    deriving (Eq, Ord, Show, Read, Typeable)


-- | Parse a 'String' as a 'SesErrorCode'.  Never fails.
toSesErrorCode :: String -> SesErrorCode
toSesErrorCode str =
    case reads ("Ses" ++ str) of
      [(r, "")] -> r
      _         -> SesOtherError str


data SesError
    = SesError {
        sesStatusCode   :: HTTP.Status
      , sesErrorCode    :: SesErrorCode
      , sesErrorMessage :: String
      }
    deriving (Show, Typeable)

instance C.Exception SesError
